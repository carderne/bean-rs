root = { SOI ~ (NEWLINE* ~ entry ~ NEWLINE*)* ~ EOI }

entry = _{
    heading
  | option
  | custom
  | commodity
  | open
  | close
  | balance
  | pad
  | price
  | document
  | transaction
  | COMMENT
  | space+
}

heading = _{ "*" ~ anyline }
option = { "option" ~ (space+ ~ quoted){2} }
custom = { date ~ space ~ "custom" ~ (space+ ~ quoted){3} }

commodity = { date ~ space+ ~ "commodity" ~ space+ ~ ccy ~ metadata_added }
open      = { date ~ space+ ~ "open" ~ space+ ~ account ~ (space+ ~ ccy)? ~ (space+ ~ quoted)? ~ metadata_added* }
close     = { date ~ space+ ~ "close" ~ space+ ~ account }

price   = { date ~ space+ ~ "price" ~ space+ ~ ccy ~ space+ ~ amount }
balance = { date ~ space+ ~ "balance" ~ space+ ~ account ~ space+ ~ amount }
pad     = { date ~ space+ ~ "pad" ~ space+ ~ account ~ space+ ~ account }

document = { date ~ space+ ~ "document" ~ account ~ space+ ~ path }

transaction   =  { date ~ space+ ~ txn_type ~ space+ ~ payee ~ (space+ ~ narration)? ~ (space+ ~ tag)? ~ (space+ ~ link)? ~ (posting_added | metadata_added)* }
payee         =  { quoted }
narration     =  { quoted }
posting_added = _{ (NEWLINE ~ posting) }
posting       =  { space+ ~ account ~ (space+ ~ amount)? }

metadata_added = _{ (NEWLINE ~ metadata) }
metadata       =  { space+ ~ key ~ ":" ~ space* ~ val }

txn_type     =  { "*" | "!" | "txn" }
key          = @{ ASCII_ALPHA_LOWER+ }
val          = @{ quoted }
path         = @{ quoted }
space        = _{ " " | "\t" }
quoted       = _{ quote ~ inner_quoted ~ quote }
inner_quoted =  { (!quote ~ ANY)* }
quote        = _{ "\"" }

date  = @{ year ~ "-" ~ month ~ "-" ~ day }
year  = @{ '1'..'2' ~ ASCII_DIGIT ~ ASCII_DIGIT ~ ASCII_DIGIT }
month = @{ '0'..'1' ~ ASCII_DIGIT }
day   = @{ '0'..'3' ~ ASCII_DIGIT }

amount = { number ~ space+ ~ ccy }
number = @{ "-"? ~ ASCII_DIGIT ~ (ASCII_DIGIT | ".")* }
ccy    = @{ ASCII_ALPHA_UPPER{3, 9} }

account      = @{ account_root ~ (":" ~ ASCII_ALPHA_UPPER ~ ASCII_ALPHA_LOWER+)+ }
account_root = _{ "Assets" | "Liabilities" | "Income" | "Expenses" | "Equity" }

tag  = @{ "#" ~ ASCII_ALPHA_LOWER+ }
link = @{ "^" ~ ASCII_ALPHA_LOWER+ }

COMMENT = _{ NEWLINE? ~ space* ~ ";" ~ anyline }
anyline = _{ (!NEWLINE ~ ANY)* }
